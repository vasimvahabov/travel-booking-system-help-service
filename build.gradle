plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.11.3'
}

group = 'com.travelbookingsystem'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.38'
    mapstructVersion = '1.6.3'
    springCloudVersion = '2025.0.0'
    applicationName = 'help-service'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation 'org.springframework.cloud:spring-cloud-function-web'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

def checkPropertiesNotEmpty = { String task, def props ->
    if (props.isEmpty()) {
        throw new GradleException("task failed: No -P parameters passed...")
    }
}

def checkProperty = { String task, String prop, def props ->
    if(!props.containsKey(prop) || !project.property(prop).toString()) {
        throw new GradleException("$task failed : $prop is not passed as a parameter...");
    }
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

tasks.named('bootBuildImage').configure {
    doFirst {
        checkPropertiesNotEmpty(name, project.gradle.startParameter.projectProperties)

        def props = project.gradle.startParameter.projectProperties
        checkProperty(name, 'version', props)

        if (project.findProperty("publishImage")?.toBoolean()) {
            ['registryUsername', 'registryToken', 'registryUrl'].each { prop ->
                checkProperty(name, prop, props)
            }
        }
    }

    builder = "docker.io/paketobuildpacks/builder-jammy-tiny"
    version = project.findProperty("version")
    imageName = "ghcr.io/vasimvahabov/help-service" + ":" + version
    publish = project.findProperty("publishImage")?.toBoolean() ?: false
    environment = [
            "BP_JVM_VERSION": JavaVersion.current().majorVersion,
            "BP_NATIVE_IMAGE": "true"
    ]
    verboseLogging = true

    docker {
        publishRegistry {
            username = project.findProperty("registryUsername")
            password = project.findProperty("registryToken")
            url = project.findProperty("registryUrl")
        }
    }

}

tasks.named('test') {
    useJUnitPlatform()
}
